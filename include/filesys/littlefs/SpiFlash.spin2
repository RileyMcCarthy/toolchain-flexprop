'' simple SPI flash memory driver
' uses no cog, no streamer, no speed optimizations
' currently hardcoded to boot ROM pins

' tested to work with the following flash ICs:
' Winbond W25Q80DV
' Issi IS25LP080D
' Amic A25L080M
' ON LE25V40
' Adesto ST25SF041

' author: Nicolas Benezan (nicolas@benezan.de, forum: ManAtWork)

' bene 07-May-2020 Spi_WrLong() fixed (little endian)
' bene 09-Jul-2020 Spi_RdByte() fixed, waitx
' bene 14-Oct-2021 smart pin synchronous serial mode instead of bit banging

' 12-Mar-2023 Modified for use with FlexC VFS by Eric Smith
'
' It is assumed that the flash chip supports at least 50MHz SPI clock.
' However, the actual SPI clock is sysclock/8 max. because the delay of the 
' smart pin IO flipflops causes trouble at sysclock/6 and higher.
' That would require special case handling.

' 24-Aug-2023 Modified for locked sysclock/4 by Evan Hillas, now reliable at high sysclock



CON     spi_cs = 61
        spi_ck = 60
        spi_di = 59 ' P2 -> flash
        spi_do = 58 ' flash -> P2

        modeCLK = P_TRANSITION | P_OE | P_INVERT_OUTPUT     ' SPI_CK
        modeTXD = P_SYNC_TX | P_INVERT_B | P_OE | ((spi_ck - spi_di) & %111)<<24 ' + P_SYNC_IO + P_FILT0_AB
        modeRXD = P_SYNC_RX |((spi_ck - spi_do) & %111)<<24

        Page_Size    = 256

        Write_Disable = $04
        Write_Enable = $06
        Erase_4k     = $20
        Erase_64k    = $D8
        Write_Page   = $02
        Read_Data    = $03
        Read_Status  = $05
        Reset_Enable = $66
        Reset_Device = $99
        Program_Resume = $7a
        PD_Release   = $ab


VAR
    LONG g_offset
    LONG g_max_addr
    LONG g_erase_size


PUB dummy() ' Dummy to avoid compiling as top
  Spi_Init()
  Spi_Cmd8 (Write_Disable)
  pinh (spi_cs)
  repeat


' initialize the object
'  start = starting address of region to use in flash (must be multiple of erase_size)
'  size  = max size to use (must be multiple of erase_size)
'  erase_size = size to be used to erase blocks
'
PUB Init(start, size, erase_size)
  g_offset := start
  g_max_addr := start + size - Page_Size
  g_erase_size := erase_size

  Spi_Init()
  Spi_Cmd8(PD_Release)
  pinh (spi_cs)
  waitms(1)
  Spi_Cmd8(Reset_Enable)
  Spi_Cmd8(Reset_Device)
  Spi_Wait()


PUB Sync() : r
  return 0


PUB Erase(flashAdr) : r | cmd, size
' size can be 4kB or 64kB
  flashAdr += g_offset
  if flashAdr > g_max_addr
    return
  size:= g_erase_size
  cmd:= Erase_4k
  if size > $1000
    cmd:= Erase_64k
  Spi_Init()
  Spi_Cmd8 (Write_Enable)
  Spi_Cmd32 (cmd, flashAdr)
  Spi_Wait()
  return 0


PUB WritePage(hubAdr, flashAdr) : r
' writes a page of 256 bytes
  flashAdr += g_offset
  if flashAdr > g_max_addr
    return 0
  Spi_Init()
  Spi_Cmd8(Write_Enable)
  Spi_Cmd32(Write_Page, flashAdr)
  repeat 64
    Spi_Wr4Bytes (long[hubAdr])
    hubAdr+= 4
  Spi_Wait()
  return 0


PUB Read (hubAdr, flashAdr, size) : r | data
' read any number of bytes
  if size == 0
    return 0
  flashAdr += g_offset
  Spi_Init ()
  Spi_Cmd32 (Read_Data, flashAdr)
  'repeat size
  '  byte[hubAdr++]:= Spi_RdByte ()
  ORG
.loop
                wxpin   #7 | 1<<5, #spi_do             ' 8 bits, with hold time (post-clock sampling)
                wypin   #16,#spi_ck                    ' start 16 clock transitions
                dirh    #spi_do                        ' enable RX smart pin
.waitDone       testp   #spi_do   wz                   ' IN=1 if byte received
        if_nz   jmp     #.waitDone                     ' wait until byte received
                rdpin   data,#spi_do
                rev     data
                zerox   data,#7                        ' limit to 8 bits
                dirl    #spi_do
		wrbyte	data, hubAdr
		add	hubAdr, #1
		djnz	size, #.loop
  END

  pinh (spi_cs)
  return 0


PUB Verify(hubAdr, flashAdr, size) : ok
' compare HUBRAM to flash contents, true means match
  flashAdr += g_offset
  ok:= true
  Spi_Init()
  Spi_Cmd32(Read_Data, flashAdr)
  repeat size
    ok&= byte[hubAdr++] == Spi_RdByte()
  pinh (spi_cs)
  return ok


PRI Spi_Init()
  ORG
                drvh    #spi_cs
                fltl    #spi_ck                        ' reset smart pins
                fltl    #spi_di
                fltl    #spi_do
                wrpin   ##modeClk,#spi_ck              ' smart pin transition mode
                wrpin   ##modeTXD,#spi_di              ' smart pin synchronous serial transmit
                wrpin   ##modeRXD,#spi_do              ' smart pin synchronous serial receive
                wxpin   #2,#spi_ck                     ' clock transition base period (sysclock/4)
                dirh    #spi_ck                        ' enable smart pin
  END


PRI Spi_Cmd8(cmd)
' outputs 8 bits command, MSB first
  ORG
                outh    #spi_cs
                waitx   #6                             ' 12+6 = 18 ticks (50 ns minimum CS deselect for write commands)
                shl     cmd,#24                        ' shift command up
                rev     cmd
                wxpin   #7, #spi_di                    ' 8 bits, continuous mode
                wypin   cmd,#spi_di
                outl    #spi_cs
                wypin   #16,#spi_ck                    ' start CLK sequence (16 transitions = 8 pulses)
                dirh    #spi_di                        ' enable TX smart pin
.waitRdy        testp   #spi_ck wz                     ' IN=1 if clocking finished
        if_nz   jmp     #.waitRdy                      ' wait until last CLK sequence finished
                dirl    #spi_di
  END


PRI Spi_Cmd32(cmd, adr)
' outputs 4 bytes: 8 bit command + 24 bits adr
  ORG
                outh    #spi_cs
                waitx   #4                             ' 14+4 = 18 ticks (50 ns minimum CS deselect for write commands)
                shl     cmd,#24                        'shift command up
                or      cmd,adr                        'or in address
                rev     cmd
                wxpin   #31, #spi_di                   ' 32 bits, continuous mode
                wypin   cmd,#spi_di
                outl    #spi_cs
                wypin   #64,#spi_ck                    ' start CLK sequence (64 transitions = 32 pulses)
                dirh    #spi_di                        ' enable TX smart pin
.waitRdy        testp   #spi_ck wz                     ' IN=1 if clocking finished
        if_nz   jmp     #.waitRdy                      ' wait until last CLK sequence finished
                dirl    #spi_di
  END


PRI Spi_Wr4Bytes(data)
' same as WrLong() but LSB first (little endian)
  ORG
                movbyts data,#%00_01_10_11
                rev     data
                wxpin   #31, #spi_di                   ' 32 bits, continuous mode
                wypin   data,#spi_di
                wypin   #64,#spi_ck                    ' start CLK sequence (64 transitions = 32 pulses)
                dirh    #spi_di                        ' enable TX smart pin
.waitRdy        testp   #spi_ck wz                     ' IN=1 if clocking finished
        if_nz   jmp     #.waitRdy                      ' wait until last CLK sequence finished
                dirl    #spi_di
  END


PRI Spi_RdByte(): data
' read 8 bits
  ORG
                wxpin   #7 | 1<<5, #spi_do             ' 8 bits, with hold time (post-clock sampling)
                wypin   #16,#spi_ck                    ' start 16 clock transitions
                dirh    #spi_do                        ' enable RX smart pin
.waitDone       testp   #spi_do   wz                   ' IN=1 if byte received
        if_nz   jmp     #.waitDone                     ' wait until byte received
                rdpin   data,#spi_do
                rev     data
                zerox   data,#7                        ' limit to 8 bits
                dirl    #spi_do
  END


PRI Spi_Wait() | st
' waits until busy flag == 0, drives spi_cs high when done
  repeat
    Spi_Cmd8(Read_Status)
    st:= Spi_RdByte()
  until st & $01 == 0
  pinh (spi_cs)

{
+------------------------------------------------------------------------------------------------------------------------------+
¦                                                   TERMS OF USE: MIT License                                                  ¦                                                            
+------------------------------------------------------------------------------------------------------------------------------¦
¦Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation    ¦ 
¦files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,    ¦
¦modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software¦
¦is furnished to do so, subject to the following conditions:                                                                   ¦
¦                                                                                                                              ¦
¦The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.¦
¦                                                                                                                              ¦
¦THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE          ¦
¦WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR         ¦
¦COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,   ¦
¦ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                         ¦
+------------------------------------------------------------------------------------------------------------------------------+
}
